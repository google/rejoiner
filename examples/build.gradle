apply plugin: 'java'
apply plugin: 'com.google.protobuf'

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    // ASSUMES GRADLE 2.12 OR HIGHER. Use plugin version 0.7.5 with earlier
    // gradle versions
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.1'
  }
}

repositories {
  mavenCentral()
  mavenLocal()
  maven {
    url 'https://oss.sonatype.org/content/repositories/snapshots/'
  }
}

// IMPORTANT: You probably want the non-SNAPSHOT version of gRPC. Make sure you
// are looking at a tagged version of the example and not "master"!

// Feel free to delete the comment at the next line. It is just for safely
// updating the version in our release process.
def grpcVersion = '1.7.0' // CURRENT_GRPC_VERSION

dependencies {
  compile 'com.google.guava:guava:24.1-jre'
  compile "com.google.api.grpc:proto-google-common-protos:0.1.9"
  compile "io.grpc:grpc-netty:${grpcVersion}"
  compile "io.grpc:grpc-protobuf:${grpcVersion}"
  compile "io.grpc:grpc-stub:${grpcVersion}"
  compile "com.google.api.graphql:rejoiner:0.1.0"
  compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.3.8.v20160314'
  compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.2'
  compile 'com.google.inject.extensions:guice-servlet:4.2.0'
  compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.3.8.v20160314'
  compile 'net.javacrumbs.future-converter:future-converter-java8-guava:1.1.0'
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.4.0'
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {
        // To generate deprecated interfaces and static bindService method,
        // turn the enable_deprecated option to true below:
        option 'enable_deprecated=false'
      }
    }
  }
}

// Inform IntelliJ projects about the generated code.
apply plugin: 'idea'

idea {
  module {
    // Not using generatedSourceDirs because of
    // https://discuss.gradle.org/t/support-for-intellij-2016/15294/8
    sourceDirs += file("${projectDir}/build/generated/source/proto/main/java");
    sourceDirs += file("${projectDir}/build/generated/source/proto/main/grpc");
  }
}

// Provide convenience executables for trying out the examples.
apply plugin: 'application'

startScripts.enabled = false

// ------------------
// Helloworld Example

task helloworldGraphqlServer(type: CreateStartScripts) {
  mainClassName = 'com.google.api.graphql.examples.helloworld.graphqlserver.GraphQlServer'
  applicationName = 'helloworld-graphqlserver'
  outputDir = new File(project.buildDir, 'tmp')
  classpath = jar.outputs.files + project.configurations.runtime
}

task helloworldBackend(type: CreateStartScripts) {
  mainClassName = 'com.google.api.graphql.examples.helloworld.backend.HelloWorldServer'
  applicationName = 'helloworld-backend'
  outputDir = new File(project.buildDir, 'tmp')
  classpath = jar.outputs.files + project.configurations.runtime
}

// ------------------
// Library Example

task libraryGraphqlServer(type: CreateStartScripts) {
  mainClassName = 'com.google.api.graphql.examples.library.graphqlserver.GraphQlServer'
  applicationName = 'library-graphqlserver'
  outputDir = new File(project.buildDir, 'tmp')
  classpath = jar.outputs.files + project.configurations.runtime
}

task bookBackend(type: CreateStartScripts) {
  mainClassName = 'com.google.api.graphql.examples.library.bookbackend.BookServer'
  applicationName = 'book-backend'
  outputDir = new File(project.buildDir, 'tmp')
  classpath = jar.outputs.files + project.configurations.runtime
}

task shelfBackend(type: CreateStartScripts) {
  mainClassName = 'com.google.api.graphql.examples.library.shelfbackend.ShelfServer'
  applicationName = 'shelf-backend'
  outputDir = new File(project.buildDir, 'tmp')
  classpath = jar.outputs.files + project.configurations.runtime
}

// ------------------
// Streaming Examples

task streamingBackend(type: CreateStartScripts) {
  mainClassName = 'com.google.api.graphql.examples.streaming.backend.HelloWorldServer'
  applicationName = 'streaming-backend'
  outputDir = new File(project.buildDir, 'tmp')
  classpath = jar.outputs.files + project.configurations.runtime
}

task streamingGraphQlClient(type: CreateStartScripts) {
  mainClassName = 'com.google.api.graphql.examples.streaming.graphqlclient.GraphQlGrpcClient'
  applicationName = 'streaming-graphql-client'
  outputDir = new File(project.buildDir, 'tmp')
  classpath = jar.outputs.files + project.configurations.runtime
}

task streamingGraphQlServer(type: CreateStartScripts) {
  mainClassName = 'com.google.api.graphql.examples.streaming.graphqlserver.GraphQlGrpcServer'
  applicationName = 'streaming-graphql-server'
  outputDir = new File(project.buildDir, 'tmp')
  classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into('bin') {
  from(helloworldGraphqlServer)
  from(helloworldBackend)
  from(libraryGraphqlServer)
  from(bookBackend)
  from(shelfBackend)
  from(streamingBackend)
  from(streamingGraphQlClient)
  from(streamingGraphQlServer)
  fileMode = 0755
}
